apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

group = 'com.trigonic'
version = '0.6'

def props = new Properties();
def localProperties = new File("local.properties")
if (localProperties.exists()) localProperties.withInputStream { props.load(it) }
props.each { project.ext.setProperty(it.key, it.value) }

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'junit:junit:4.+'
    compile 'com.google.guava:guava:13.0'
    compile "org.springframework:spring-core:3.1.2.RELEASE"

    deployerJars 'org.apache.maven.wagon:wagon-http:1.0-beta-7'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: props.get('maven.repository.user'), password: props.get('maven.repository.password'))
        }

        pom.project {
            name 'test-utils - Test Utilities'
            packaging 'jar'
            description 'Provides utilities for testing'
            url 'https://github.com/TrigonicSolutions/test-utils'
            inceptionYear '2011'

            parent {
                groupId 'org.sonatype.oss'
                artifactId 'oss-parent'
                version '7'
            }
     
            scm {
                url 'scm:git://github.com/TrigonicSolutions/test-utils.git'
                connection 'scm:git://github.com/TrigonicSolutions/test-utils.git'
            }
     
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
     
            developers {
                developer {
                    id 'akrueger'
                    name 'Alan Krueger'
                    email 'alan@trigonic.com'
                    roles{
                        role 'Developer'
                    }
                    timezone '-6'
                }
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

